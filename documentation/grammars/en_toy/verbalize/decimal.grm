import '../byte.grm' as b;
import '../util.grm' as u;


# 10.5000
#
# into
#
# 'ten point five zero zero zero' or 'ten point five' or 'ten point fifty' or 'ten fifty'



func decimal_markup[integer_part_match,sep_string,fractional_part_match] {
    q = u.q;

    # Used to allow for different numbers of spaces coming out of the serializer.
    s = u.s;

    decimal =
      u.D["decimal" s "{" s]
      u.D["integer_part:" s q]
      integer_part_match
      u.I[sep_string]
      u.D[q s]
      u.D["fractional_part:" s q]
      fractional_part_match
      u.D[q s]
      u.D["}"]
    ;

    return decimal;
}

sigma_star = b.kBytes*;
cardinal = LoadFst['CARDINAL_NUMBER_NAME'];
d = b.kDigit;

# DECIMAL LITERAL

insspace = "" : " ";
zero  = "0" : "zero";

units =   ("1" : "one")
        | ("2" : "two")
        | ("3" : "three")
        | ("4" : "four")
        | ("5" : "five")
        | ("6" : "six")
        | ("7" : "seven")
        | ("8" : "eight")
        | ("9" : "nine")
;

read_digit_by_digit = (units | zero) (insspace (units | zero))* ;


##
# DECIMAL COLLOQUIAL 1

remove_leading_spaces      = CDRewrite[u.D["0"]*, "[BOS]", "", sigma_star];
remove_all_trailing_spaces = CDRewrite[u.D["0"]*, "", "[EOS]", sigma_star];
strip_zeros                = remove_leading_spaces @ remove_all_trailing_spaces;

##
# DECIMAL COLLOQUIAL 2

keep_two_fractional_sigfigs  = CDRewrite[u.D["0"]*, "[BOS]" d{2}, "[EOS]", sigma_star];
strip_zeros_two_sigfigs      = remove_leading_spaces @ keep_two_fractional_sigfigs; 


                                                                                                        # orig: 2.500
export DECIMAL              = decimal_markup[cardinal, " point ", read_digit_by_digit];                 # two point five zero zero
export DECIMAL_COLLOQUIAL_1 = decimal_markup[cardinal, " point ", strip_zeros @ read_digit_by_digit];   # two point five 
export DECIMAL_COLLOQUIAL_2 = decimal_markup[cardinal, " point ", strip_zeros_two_sigfigs @ cardinal];  # two point fifty
export DECIMAL_COLLOQUIAL_3 = decimal_markup[cardinal, " "      , strip_zeros_two_sigfigs @ cardinal];  # two fifty


