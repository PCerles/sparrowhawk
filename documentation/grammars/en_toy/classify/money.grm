import '../byte.grm' as b;
import '../util.grm' as u;

d = b.kDigit;
q = u.q;
currencies = StringFile['money.tsv'];

# Maps input of the form
#
# $2.50
#
# into
#
# money { currency: "usd" amount { integer_part: "2" fractional_part: "50"} } }
#
# The fractional part is optional.

thousand = ("K" | "k") | (("T" | "t") ("H" | "h") ("O" | "o") ("U" | "u") ("S" | "s") ("A" | "a") ("N" | "n") ("D" | "d"));


illion  = ("I" | "i") ("L" | "l") ("L" | "l") ("I" | "i") ("O" | "o") ("N" | "n");

million  = ("M" | "m") illion?;
billion  = ("B" | "b") illion?;
trillion = ("T" | "t") (("R" | "r") illion)?;

money =
  u.I["money { "]
  u.I["currency: " q]
  currencies
  u.I[q]
  u.I[" amount { "]
  u.I["integer_part: " q]
  d+
  u.I[q]
 (u.D["."]
  u.I[" fractional_part: " q]
  d+
  u.I[q])?

   (  (u.D[thousand] u.I[" quantity: 0 "])
    | (u.D[million]  u.I[" quantity: 1 "])
    | (u.D[billion]  u.I[" quantity: 2 "])
    | (u.D[trillion] u.I[" quantity: 3 "]) )?

  u.I["} }"]
;

export MONEY = Optimize[money];
